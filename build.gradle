/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

group = 'com.example.elasticagent.template'
version = '0.4-SNAPSHOT'

// these values that go into plugin.xml
// TODO: change these
project.ext.pluginDesc = [
    id         : 'com.example.elasticagent.foocloud',
    version    : project.version,
    goCdVersion: '19.3.0',
    name       : 'Example Elastic Agent Plugin',
    description: 'Some longer description of your plugin goes here',
    vendorName : 'Acme Corp',
    vendorUrl  : 'https://github.com/user/repository'
]

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

// In this section you declare where to find the dependencies of your project
repositories {
  mavenCentral()
}

ext {
  deps = [
    gocdPluginApi: 'cd.go.plugin:go-plugin-api:25.1.0',
    mockito      : 'org.mockito:mockito-core:5.16.0',
  ]

  versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
  compileOnly project.deps.gocdPluginApi

  implementation group: 'com.google.code.gson', name: 'gson', version: '2.12.1'
  implementation group: 'com.google.guava', name: 'guava', version: '23.0'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.17.0'
  implementation group: 'joda-time', name: 'joda-time', version: '2.13.1'

  testImplementation platform('org.junit:junit-bom:5.12.0')
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
  testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: project.versions.mockito
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: project.versions.mockito
  testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '3.0'
  testImplementation group: 'cd.go.plugin', name: 'go-plugin-api', version: project.versions.gocdPluginApi
  testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.3'
}

processResources {
  duplicatesStrategy(DuplicatesStrategy.INCLUDE)
  from("src/main/resource-templates") {
    filesMatching('plugin.xml') {
      expand project.pluginDesc
    }
  }

  from("src/main/resources") {
    filesMatching('plugin.properties') {
      expand project.pluginDesc
    }
  }
}

test {
  useJUnitPlatform()
  testLogging {
    showStandardStreams = true

    exceptionFormat 'full'

    beforeSuite { suite ->
      if (suite.parent) {
        logger.quiet("Running ${suite.name}")
      }
    }

    afterSuite { suite, result ->
      if (suite.parent) {
        logger.quiet("Tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
        if (result.resultType == TestResult.ResultType.FAILURE) {
          logger.quiet("Test ${suite.name} FAILED")
        }
      } else {
        logger.quiet("Total tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
      }
    }
  }
}

jar {
  from(configurations.runtimeClasspath) {
    into "lib/"
  }
}
